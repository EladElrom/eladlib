<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" minWidth="955" minHeight="600"
			   creationComplete="application1_creationCompleteHandler(event)">
	
	<fx:Script>
		<![CDATA[
			import com.elad.twitter.TwitterClient;
			import com.elad.twitter.events.TwitterHelperFailureEvent;
			import com.elad.twitter.events.TwitterHelperSuccessEvent;
			import com.elad.twitter.vo.TweetVO;
			
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			
			/**
			 *  Twitter helper utility class instance
			 */		
			public var twitterClient:TwitterClient;
			
			/**
			 *   Holds a timer so we will be able to update list
			 */		
			public var timer:Timer;			
			

			protected function application1_creationCompleteHandler(event:FlexEvent):void
			{
				twitterClient = new TwitterClient();
				twitterClient.addEventListener( TwitterHelperSuccessEvent.RETRIEVE_TWEETS, onRetrieveTweets );
				twitterClient.addEventListener( TwitterHelperFailureEvent.SERVICE_FAILURE, onFaultRequest );
				
				retrieveTweetsEveryFewSeconds( 4 );
			}
			
			/**
			 * Method to go and retireve tweets every defined number of seconds
			 *  
			 * @param seconds
			 * 
			 */		
			public function retrieveTweetsEveryFewSeconds( seconds:int ):void
			{
				timer = new Timer( seconds*1000, 100000 );
				timer.addEventListener( TimerEvent.TIMER, onTimerHandler, false, 0, true );
				
				timer.start();
			}
			
			//--------------------------------------------------------------------------
			//
			//  Handlers
			//
			//--------------------------------------------------------------------------					
			
			/**
			 * Method to handle a timer event
			 *  
			 * @param event
			 * 
			 */		
			private function onTimerHandler( event:TimerEvent ):void
			{
				twitterClient.retrieveTweetsBasedOnHashTag( "FlashAndTheCity" );
			}
			
			/**
			 * Handler for results
			 *  
			 * @param event
			 * 
			 */		
			private function onRetrieveTweets( event:TwitterHelperSuccessEvent ):void
			{
				var dataProvider:Array = new Array();
				
				event.collection.forEach( function callback(item:TweetVO, index:int, vector:Vector.<TweetVO>):void {
					dataProvider.push( item );
				} );
				
				dataGrid.dataProvider = dataProvider;
			}
			
			/**
			 * Handler for fault
			 *  
			 * @param event
			 * 
			 */		
			private function onFaultRequest( event:TwitterHelperFailureEvent ):void
			{
				Alert.show("onFaultRequest");
			}			

		]]>
	</fx:Script>
	
	<mx:DataGrid id="dataGrid" width="500" height="400" />
	
</s:Application>
